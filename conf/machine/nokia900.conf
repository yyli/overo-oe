#@TYPE: Machine
#@NAME: Nokia 900 internet tablet
#@DESCRIPTION: Machine configuration for the Nokia 900

TARGET_ARCH = "arm"
SOC_FAMILY = "omap3"
require conf/machine/include/tune-cortexa8.inc


PREFERRED_PROVIDER_virtual/xserver = "xserver-xorg"
XSERVER = "xserver-xorg \
           xf86-input-evdev \
           xf86-video-fbdev \
           xf86-input-keyboard \
           xf86-video-omapfb "

MACHINE_EXTRA_RRECOMMENDS = "\
  watchdog \
  kernel-module-g-nokia \
  kernel-module-tsl2563 \
  kernel-module-smc91x \
  kernel-module-leds-lp5523 \
  kernel-module-adp1653 \
  kernel-module-ad5820 \
  kernel-module-et8ek8 \
  kernel-module-fmtx-si4713 \
  kernel-module-leds-twl4030-vibra \
  kernel-module-lis302dl \
  kernel-module-nokia-av \
  kernel-module-omap-ssi \
  kernel-module-omap-wdt \
  kernel-module-phonet \
  kernel-module-radio-bcm2048 \
  kernel-module-rtc-twl4030 \
  kernel-module-smia-sensor \
  kernel-module-ssi-mcsaab-imp \
  kernel-module-tsc2005 \
  kernel-module-twl4030-wdt \
  kernel-module-wl12xx \
  kernel-module-cmt-speech \
  kernel-module-gpio-keys \
  kernel-module-hci-h4p \
  kernel-module-hsi-char \
  kernel-module-isp1704-charger \
  kernel-module-lis3lv02d-i2c \
  kernel-module-mailbox-mach \
  kernel-module-rtc-twl \
  kernel-module-twl4030-pwrbutton \
  kernel-module-wl1251-spi \
  kernel-module-ubifs \
"

GUI_MACHINE_CLASS = "bigscreen"
MACHINE_DISPLAY_WIDTH_PIXELS = "800"
MACHINE_DISPLAY_HEIGHT_PIXELS = "480"

KERNEL_IMAGETYPE = "uImage"

UBOOT_ENTRYPOINT = "0x80008000"
UBOOT_LOADADDRESS = "0x80008000"
EXTRA_IMAGEDEPENDS += "u-boot"

ROOT_FLASH_SIZE = "100"
EXTRA_IMAGECMD_jffs2_nokia900 = "--pad --little-endian --eraseblock=0x20000 -n"

# Add "rootfstype=ubifs ubi.mtd=5,2048 root=ubi0:nokia900-rootfs"
# to the kernel cmdline to boot an ubifs image flashed on /dev/mtd5
# Here are the arguments with the corresponding dmesg messages :
# -m: UBI: smallest flash I/O unit:    2048
# -e: UBI: logical eraseblock size:    126976 bytes
# -c: UBI: number of good PEBs:        2009
# -p: UBI: physical eraseblock size:   131072 bytes (128 KiB)
# -x: best compression ratio
# -s and -O : set to the smallest flash I/O unit to bypass the subpages
# See http://www.linux-mtd.infradead.org/faq/ubi.html#L_vid_offset_mismatch for more details
# else you have some errors like theses in dmesg with ubiattach:
# onenand_write_ops_nolock: verify failed -74
# UBI error: ubi_io_write: error -74 while writing 512 bytes to PEB 961:512, written 512 bytes
# UBI warning: ubi_eba_write_leb: failed to write VID header to LEB 2147479551:0, PEB 961
# UBI warning: ubi_ro_mode: switch to read-only mode
MKUBIFS_ARGS = "-m 2048 -e 126976 -c 2009 -x zlib"
UBINIZE_ARGS = "-m 2048 -p 131072 -s 2048 -O 2048"

IMAGE_FSTYPES ?= "jffs2 tar.gz"

PREFERRED_PROVIDER_virtual/kernel = "linux"

#use this if you are using the nokia initfs
ROOTFS_POSTPROCESS_COMMAND += " remove_init_link; "

MACHINE_FEATURES = "kernel26 apm alsa bluetooth usbgadget vfat keyboard"

module_autoload_g_ether = "g_ether"

# Copied from include/omap3.inc (as we do not use linux-omap recipes):
#
# Increase this everytime you change something in the kernel
MACHINE_KERNEL_PR = "r58"
