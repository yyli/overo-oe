# This describes a generic SlugOS image, even though the bb file is
# called 'slugos-image.bb' the distro specific configuration is
# done in conf/distro/${DISTRO}.conf (which should always include
# conf/distro/slugos.conf to get the standard settings).

HOMEPAGE = "http://www.nslu2-linux.org"

DEPENDS = "task-slugos"

# HACK - added dependency on module-init-tools-cross so that it is
# forced to be built (not sure why bitbake (1.10.2) is missing the
# dependency generated by the kernel recipes) - MJW 26-Dec-2010
DEPENDS += "module-init-tools-cross"
# End of HACK

IMAGE_INSTALL = "task-slugos"

COMPATIBLE_MACHINE = "(nslu2|ixp4xx|sheevaplug|qemuarm)"

IMAGE_NAME = "${IMAGE_BASENAME}-${DISTRO_VERSION}-${MACHINE}"
IMAGE_LINK_NAME = "${IMAGE_BASENAME}-${MACHINE}"
IMAGE_FSTYPES = "jffs2 tar.gz ext2.gz"
IMAGE_DEVICE_TABLES = "files/device_table-slugos.txt"
IMAGE_LINGUAS = ""

# Override the INIT manager so that we don't pull in the unnecessary
# sysvinit-pidof and it's dependencies (sysvinit-utils).
IMAGE_INIT_MANAGER = "sysvinit"

# Override this when/if SlugOS switches to busybox for login
# (tinylogin is pretty old, and no longer maintained)
# IMAGE_LOGIN_MANAGER = "tinylogin"

# IMAGE_PREPROCESS_COMMAND is run before making the image.  In SlugOS the
# kernel image is removed from the root file system to recover the space used -
# SlugOS is assumed to boot from a separate kernel image in flash (not in the
# root file system), if this is not the case the following must not be done!
IMAGE_PREPROCESS_COMMAND += "rm -f ${IMAGE_ROOTFS}/boot/?Image*;"

inherit image
